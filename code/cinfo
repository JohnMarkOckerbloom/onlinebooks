#!/usr/bin/perl

use lib "nlib";
use lib "/websites/OnlineBooks/nonpublic/lib";
use CGI;
use OLBP;
use OLBP::CopyrightInfo;
use OLBP::AuthorRights;
use OLBP::WikidataSource;
use OLBP::Hash;
use CGI::Carp qw(fatalsToBrowser);
use strict;

my $server = "https://onlinebooks.library.upenn.edu";
my $default = $server. "/cce/";
my $cinfodir  = $OLBP::dbdir . "cinfo/";
my $idxdir  = $OLBP::dbdir . "indexes/";
my $contactfile = $cinfodir . "contacts";

my $scriptname = '/webbin/cinfo/';
my $redirfile = $cinfodir . 'REDIRECTS';

sub olbp_entry_title {
  my ($arg) = @_;
  my $hashname = "serials";
  my $fname = OLBP::hashfilename($hashname, $idxdir);
  my $hash = new OLBP::Hash(name=>$hashname, filename=>$fname, cache=>1);
  my $str = $hash->get_value(key=>$arg);
  return undef if (!$str);
  my @lines = split /\n/, $str;
  while (my $line = shift @lines) {
    if ($line =~ /^TITLE\s+(.*)/) {
      return $1;
    }
  }
  return undef;
}

sub redirect {
  my ($id, $format) = @_;
  open my $fh, "< $redirfile" or return 0;
  while (my $line = <$fh>) {
    if ($line =~ /$id\s+(.+)/) {
      my $newid = $1;
      close $fh;
      my $url = $server . $scriptname . $newid;
      if ($format eq "json") {
        # if we get more general, be sure to sanitize input
        $url .= "?format=json";
      }
      print "Location: $url\n\n";
      exit 0;
    }
  }
  close $fh;
  return 0;
}

sub getout {
  print "Location: $default\n\n";
  exit 0;
}

my $q = new CGI;
my $arg = $q->url(-path_info=>1);
$arg =~ s/(.*)\///;
my $format = $q->param("format");


my $contacts = new OLBP::AuthorRights(path=>$contactfile);
my $wdsource = new OLBP::WikidataSource(dir=>$OLBP::dbdir);
my $info = new OLBP::CopyrightInfo(dir=>$cinfodir, arfile=>$contacts,
                                   wdsource=>$wdsource);

# validate inputs
if (!$arg || $arg =~ /[^a-zA-Z0-9\-]/) {
  getout();
}

my $fname = $cinfodir . $arg . ".json";
if (!-e $fname) {
  my $title = olbp_entry_title($arg);
  if ($title) {
    binmode STDOUT, ":utf8";
    print "Content-type: text/html\n\n";
    $info->display_olbp_cover(title=>$title, id=>$arg);
    exit 0;
  }
  if (!redirect($arg, $format)) {
    getout();
  }
}

if ($format eq "json") {
  print "Content-type: application/json\n\n";
  $info->display_json(filename=>$arg);
  exit 0;
}

binmode STDOUT, ":utf8";
print "Content-type: text/html\n\n";
$info->display_page(filename=>$arg);
